// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

directives: (directive ";")*
?directive: let | magic | reduce | draw
let: "let" VARWORD "=" expr
magic: "magic" VARWORD
reduce: "reduce" expr
draw: "draw" VARWORD

?expr: subexpr | apply
?subexpr: var | mlambda | "(" expr ")" | list
apply: expr subexpr
var: VARWORD
mlambda: ("λ" | "\\") varlist "." expr
list: "[" subexpr* "]"

varlist: VARWORD*
VARWORD: ("_"|LETTER|DIGIT|"α"|"β"|"γ"|"δ"|"ε"|"ζ"|"ω")+

COMMENT: /#[^\n]*/
%ignore WS
%ignore COMMENT

%import common.LETTER
%import common.DIGIT
%import common.WS
